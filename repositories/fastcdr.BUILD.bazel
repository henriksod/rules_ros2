""" Builds FastCDR.
"""

load("@bazel_skylib//lib:dicts.bzl", "dicts")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")

filegroup(
    name = "all_srcs",
    srcs = glob(["**"]),
)

cache_entries = {
    "CMAKE_POSITION_INDEPENDENT_CODE": "ON",  # Must be set!
    "BUILD_SHARED_LIBS": "OFF",
    # FastCDR specific options.
    "APPEND_PROJECT_NAME_TO_INCLUDEDIR": "OFF",
    "BUILD_DOCUMENTATION": "OFF",
    "CHECK_DOCUMENTATION": "OFF",
    "EPROSIMA_BUILD": "OFF",
    "EPROSIMA_BUILD_TESTS": "OFF",
    "EPROSIMA_INSTALLER": "OFF",
}

cache_entries_debug = {
    "CMAKE_BUILD_TYPE": "Debug",
}

cache_entries_release = {
    "CMAKE_BUILD_TYPE": "Release",
}

cmake(
    name = "fastcdr",
    build_args = [
        "--",
        "-j4",
    ],
    cache_entries = select(
        {
            "@com_github_mvukov_rules_ros2//ros2:build_type_debug": dicts.add(
                cache_entries,
                cache_entries_debug,
            ),
            "@com_github_mvukov_rules_ros2//ros2:build_type_release": dicts.add(
                cache_entries,
                cache_entries_release,
            ),
        },
        no_match_error = "Unsupported build type",
    ),
    lib_source = ":all_srcs",
    out_static_libs = ["libfastcdr.a"],
    visibility = ["//visibility:public"],
)
